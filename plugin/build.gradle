import org.apache.tools.ant.filters.ReplaceTokens

dependencies {
    project(":nms").getSubprojects().each {
        implementation it
    }

    // funnyguilds & panda utilities
    implementation "net.dzikoysk:funnycommands:0.3.11"
    implementation "org.panda-lang:panda-utilities:0.4.1-alpha"

    // okaeri config library
    implementation "eu.okaeri:okaeri-configs-yaml-bukkit:2.7.20"
    implementation "eu.okaeri:okaeri-configs-validator-okaeri:2.7.20"

    // general stuff
    implementation "com.zaxxer:HikariCP:4.0.3"
    implementation("com.google.guava:guava:21.0") {
        because "WorldEdit defined a constraint that we must use 21.0 and there is no way to ignore it"
    }
    implementation("com.google.code.gson:gson:2.8.0") {
        because "WorldEdit defined a constraint that we must use 2.8.0 and there is no way to ignore it"
    }
    implementation "org.apache.commons:commons-lang3:3.12.0"

    // probably fix for some exception?
    implementation "org.apache.logging.log4j:log4j-slf4j-impl:2.14.1"

    // bukkit stuff
    shadow "org.spigotmc:spigot-api:1.16.2-R0.1-SNAPSHOT"
    shadow "org.apache.logging.log4j:log4j-core:2.14.1"

    // integrations
    shadow("me.clip:placeholderapi:2.10.9") {
        exclude group: "com.google.code.gson", module: "gson"
    }
    shadow "me.robin:leaderheads-api:1.0.0"
    shadow "net.milkbowl.vault:VaultAPI:1.7"
    shadow("be.maximvdw:MVdWPlaceholderAPI:2.5.2-SNAPSHOT") {
        exclude group: "org.spigotmc", module: "spigot"
    }
    shadow "codecrafter47.bungeetablistplus:bungeetablistplus-api-bukkit:3.4.4"

    shadow "com.sk89q.worldedit:worldedit-bukkit:7.2.5"
    shadow "com.sk89q.worldguard:worldguard-bukkit:7.0.5"
}

processResources {
    filter(ReplaceTokens, tokens: [
            funnyGuildsVersion: version,
            funnyGuildsCommit: grgit.head().abbreviatedId
    ])
}

shadowJar {
    archiveFileName = "FunnyGuilds ${project.version} (MC 1.8-1.17).jar"

    relocate "net.dzikoysk.funnycommands", "net.dzikoysk.funnyguilds.libs.net.dzikoysk.funnycommands"
    relocate "org.panda_lang", "net.dzikoysk.funnyguilds.libs.org.panda_lang"
    relocate "javassist", "net.dzikoysk.funnyguilds.libs.javassist"
    relocate "com.zaxxer", "net.dzikoysk.funnyguilds.libs.com.zaxxer"
    relocate("com.google", "net.dzikoysk.funnyguilds.libs.com.google") {
        exclude "com.google.gson.**"
    }
    relocate "org.apache.commons.lang3", "net.dzikoysk.funnyguilds.libs.org.apache.commons.lang3"
    relocate "org.apache.logging", "net.dzikoysk.funnyguilds.libs.org.apache.logging"
    relocate "org.slf4j", "net.dzikoysk.funnyguilds.libs.org.slf4j"
    relocate "eu.okaeri", "net.dzikoysk.funnyguilds.libs.eu.okaeri"

    exclude "org/checkerframework/**"
    exclude "org/intellij/lang/annotations/**"
    exclude "org/jetbrains/annotations/**"

    minimize {
        exclude(dependency('net.dzikoysk:funnycommands:.*'))
        exclude(dependency('com.fasterxml.jackson.core:jackson-core:.*'))
    }
}

jar {
    enabled = false
    dependsOn 'shadowJar'
}