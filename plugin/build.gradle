import org.apache.tools.ant.filters.ReplaceTokens
import org.gradle.api.tasks.testing.logging.TestExceptionFormat
import org.gradle.api.tasks.testing.logging.TestLogEvent

dependencies {
    project(":nms").getSubprojects().each {
        implementation it
    }

    // funnyguilds & panda utilities
    api "org.panda-lang:expressible:1.1.17"
    implementation "net.dzikoysk:funnycommands:0.5.2"

    // okaeri config library
    implementation "eu.okaeri:okaeri-configs-yaml-bukkit:4.0.0-beta12"
    implementation "eu.okaeri:okaeri-configs-serdes-commons:4.0.0-beta12"
    implementation "eu.okaeri:okaeri-configs-validator-okaeri:4.0.0-beta12"

    // general stuff
    //noinspection GradlePackageUpdate
    implementation "com.zaxxer:HikariCP:4.0.3"
    //noinspection GradlePackageUpdate
    implementation("com.google.guava:guava:21.0") {
        because "WorldEdit defined a constraint that we must use 21.0 and there is no way to ignore it"
    }
    //noinspection GradlePackageUpdate
    implementation("com.google.code.gson:gson:2.8.0") {
        because "WorldEdit defined a constraint that we must use 2.8.0 and there is no way to ignore it"
    }
    implementation "org.apache.commons:commons-lang3:3.12.0"
    implementation "org.bstats:bstats-bukkit:3.0.0"

    // probably fix for some exception?
    implementation "org.apache.logging.log4j:log4j-slf4j-impl:2.17.2"

    // bukkit stuff
    shadow "org.spigotmc:spigot-api:1.16.5-R0.1-SNAPSHOT"
    shadow "org.apache.logging.log4j:log4j-core:2.17.2"

    // integrations
    shadow "com.sk89q.worldguard:worldguard-bukkit:7.0.5"
    shadow "net.milkbowl.vault:VaultAPI:1.7"
    shadow "codecrafter47.bungeetablistplus:bungeetablistplus-api-bukkit:3.4.4"
    shadow("be.maximvdw:MVdWPlaceholderAPI:3.1.1-SNAPSHOT") {
        exclude group: "org.spigotmc", module: "spigot"
    }
    shadow("me.clip:placeholderapi:2.10.9") {
        because "PlaceholderAPI on versions higher than 2.10.9 causes GH-1700 for some unknown reason"
        exclude group: "com.google.code.gson", module: "gson"
    }
    shadow "net.kyori:adventure-api:4.10.1"
    shadow "me.robin:leaderheads-api:1.0.0"
    shadow "com.gmail.filoghost.holographicdisplays:holographicdisplays-api:2.4.9"

    testImplementation 'nl.jqno.equalsverifier:equalsverifier:3.10'
    testImplementation 'org.spigotmc:spigot-api:1.16.2-R0.1-SNAPSHOT'
    testImplementation 'org.mockito:mockito-core:4.4.0'
    testImplementation 'org.mockito:mockito-junit-jupiter:4.4.0'
    testImplementation 'org.mockito:mockito-inline:4.4.0'
    testImplementation 'org.codehaus.groovy:groovy-all:3.0.10'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
    testImplementation 'org.panda-lang:expressible-junit:1.1.17'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'
}

processResources {
    filter(ReplaceTokens, tokens: [
            funnyGuildsVersion: version,
            funnyGuildsCommit: grgit.head().abbreviatedId
    ])
}

shadowJar {
    archiveFileName = "FunnyGuilds ${project.version} (MC 1.8-1.18).jar"

    relocate "net.dzikoysk.funnycommands", "net.dzikoysk.funnyguilds.libs.net.dzikoysk.funnycommands"
    relocate "panda.utilities", "net.dzikoysk.funnyguilds.libs.panda.utilities"
    relocate "javassist", "net.dzikoysk.funnyguilds.libs.javassist"
    relocate "com.zaxxer", "net.dzikoysk.funnyguilds.libs.com.zaxxer"
    relocate("com.google", "net.dzikoysk.funnyguilds.libs.com.google") {
        exclude "com.google.gson.**"
    }
    relocate "org.apache.commons.lang3", "net.dzikoysk.funnyguilds.libs.org.apache.commons.lang3"
    relocate "org.apache.logging", "net.dzikoysk.funnyguilds.libs.org.apache.logging"
    relocate "org.slf4j", "net.dzikoysk.funnyguilds.libs.org.slf4j"
    relocate "org.bstats", "net.dzikoysk.funnyguilds.libs.bstats"
    relocate "eu.okaeri", "net.dzikoysk.funnyguilds.libs.eu.okaeri"

    exclude "org/checkerframework/**"
    exclude "org/intellij/lang/annotations/**"
    exclude "org/jetbrains/annotations/**"

    minimize {
        exclude(dependency('net.dzikoysk:funnycommands:.*'))
        exclude(dependency('com.fasterxml.jackson.core:jackson-core:.*'))
    }
}

test {
    useJUnitPlatform()
    forkEvery = 1
    maxParallelForks = 4

    testLogging {
        events(TestLogEvent.STARTED, TestLogEvent.PASSED, TestLogEvent.FAILED, TestLogEvent.SKIPPED)
        exceptionFormat = TestExceptionFormat.FULL
        showExceptions = true
        showCauses = true
        showStackTraces = true
        showStandardStreams = true
    }
}
