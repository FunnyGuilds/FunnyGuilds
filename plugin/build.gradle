import org.apache.tools.ant.filters.ReplaceTokens

dependencies {
    project(":nms").getSubprojects().each {
        implementation it
    }

    // funnyguilds & panda utilities
    api "org.panda-lang:expressible:1.1.9"
    implementation "net.dzikoysk:funnycommands:0.4.2"
    implementation "org.panda-lang:panda-utilities:0.5.0-alpha"
    implementation "org.panda-lang.utilities:di:1.4.1"

    // okaeri config library
    implementation "eu.okaeri:okaeri-configs-yaml-bukkit:3.4.2"
    implementation "eu.okaeri:okaeri-configs-serdes-commons:3.4.2"
    implementation "eu.okaeri:okaeri-configs-validator-okaeri:3.4.2"

    // general stuff
    implementation "com.zaxxer:HikariCP:4.0.3"
    implementation("com.google.guava:guava:21.0") {
        because "WorldEdit defined a constraint that we must use 21.0 and there is no way to ignore it"
    }
    implementation("com.google.code.gson:gson:2.8.0") {
        because "WorldEdit defined a constraint that we must use 2.8.0 and there is no way to ignore it"
    }
    implementation "org.apache.commons:commons-lang3:3.12.0"
    implementation "org.bstats:bstats-bukkit:2.2.1"

    // probably fix for some exception?
    implementation "org.apache.logging.log4j:log4j-slf4j-impl:2.14.1"

    // bukkit stuff
    shadow "org.spigotmc:spigot-api:1.16.2-R0.1-SNAPSHOT"
    shadow "org.apache.logging.log4j:log4j-core:2.14.1"

    // integrations
    shadow("me.clip:placeholderapi:2.10.10") {
        exclude group: "com.google.code.gson", module: "gson"
        exclude group: "net.kyori", module: "adventure-platform-bukkit"
    }
    shadow "net.kyori:adventure-api:4.9.3"
    shadow "net.kyori:adventure-platform-bukkit:4.0.2-SNAPSHOT"

    shadow "me.robin:leaderheads-api:1.0.0"
    shadow "net.milkbowl.vault:VaultAPI:1.7"
    shadow("be.maximvdw:MVdWPlaceholderAPI:3.1.1-SNAPSHOT") {
        exclude group: "org.spigotmc", module: "spigot"
    }
    shadow "codecrafter47.bungeetablistplus:bungeetablistplus-api-bukkit:3.4.4"

    shadow "com.sk89q.worldedit:worldedit-bukkit:7.2.7"
    shadow "com.sk89q.worldguard:worldguard-bukkit:7.0.5"
    shadow "com.gmail.filoghost.holographicdisplays:holographicdisplays-api:2.4.9"

    testImplementation 'nl.jqno.equalsverifier:equalsverifier:3.7.2'
    testImplementation 'org.spigotmc:spigot-api:1.16.2-R0.1-SNAPSHOT'
    testImplementation 'org.mockito:mockito-core:4.1.0'
    testImplementation 'org.mockito:mockito-junit-jupiter:4.1.0'
    testImplementation 'org.mockito:mockito-inline:4.1.0'
    testImplementation 'org.codehaus.groovy:groovy-all:3.0.9'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'
}

processResources {
    filter(ReplaceTokens, tokens: [
            funnyGuildsVersion: version,
            funnyGuildsCommit: grgit.head().abbreviatedId
    ])
}

shadowJar {
    archiveFileName = "FunnyGuilds ${project.version} (MC 1.8-1.17).jar"

    relocate "net.dzikoysk.funnycommands", "net.dzikoysk.funnyguilds.libs.net.dzikoysk.funnycommands"
    relocate "panda.utilities", "net.dzikoysk.funnyguilds.libs.panda.utilities"
    relocate "javassist", "net.dzikoysk.funnyguilds.libs.javassist"
    relocate "com.zaxxer", "net.dzikoysk.funnyguilds.libs.com.zaxxer"
    relocate("com.google", "net.dzikoysk.funnyguilds.libs.com.google") {
        exclude "com.google.gson.**"
    }
    relocate "org.apache.commons.lang3", "net.dzikoysk.funnyguilds.libs.org.apache.commons.lang3"
    relocate "org.apache.logging", "net.dzikoysk.funnyguilds.libs.org.apache.logging"
    relocate "org.slf4j", "net.dzikoysk.funnyguilds.libs.org.slf4j"
    relocate "org.bstats", "net.dzikoysk.funnyguilds.libs.bstats"
    relocate "eu.okaeri", "net.dzikoysk.funnyguilds.libs.eu.okaeri"

    exclude "org/checkerframework/**"
    exclude "org/intellij/lang/annotations/**"
    exclude "org/jetbrains/annotations/**"

    minimize {
        exclude(dependency('net.dzikoysk:funnycommands:.*'))
        exclude(dependency('com.fasterxml.jackson.core:jackson-core:.*'))
    }
}

test {
    useJUnitPlatform()
    forkEvery = 1
    maxParallelForks = 4
}
